plugins {
	id 'com.jfrog.artifactory' version '4.21.0' apply false
	id 'io.spring.dependency-management' version '1.1.4'
	id 'java'
    id 'maven-publish'
}


subprojects {
	apply plugin: 'java-library'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.artifactory'
	apply plugin: 'signing'

	sourceCompatibility = '17'

	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'https://repo.spring.io/milestone' }
		if (version.endsWith('SNAPSHOT')) {
			maven { url 'https://repo.spring.io/snapshot' }
			maven {
				url 'https://oss.jfrog.org/artifactory/oss-snapshot-local'
			}
		}
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
		}

		dependencies {
			dependency "org.assertj:assertj-core:${assertjVersion}"
		}
	}

	dependencies {
		implementation platform("io.projectreactor:reactor-bom:${reactorBomVersion}")
		implementation platform("io.rsocket:rsocket-bom:${rsocketVersion}")
		implementation platform("org.junit:junit-bom:${junitJupiterVersion}")
	}

	java {
		withSourcesJar()
		withJavadocJar()
	}
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
			}
		}
	}

	test {
		useJUnitPlatform()
	}

}

apply from: "${rootDir}/gradle/publications.gradle"

description = 'RSocket Broker Client: client for RSocket Broker.'

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}
